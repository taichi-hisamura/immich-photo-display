plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.ksp)
    alias(libs.plugins.hilt)
    alias(libs.plugins.roborazzi)
}

android {
    namespace = "com.dav3.immichframe"
    compileSdk = 37
    compileSdkMinor = 0

    defaultConfig {
        applicationId = "com.familyphotoframe.immichframe.lowbandwidth"
        minSdk = 26
        targetSdk = 37
        versionCode = (System.currentTimeMillis() / 1000).toInt()
        versionName = "0.5.0"

        // Git SHA for self-update version comparison (config-cache safe)
        buildConfigField(
            "String",
            "GIT_SHA",
            "\"${providers.exec {
                commandLine("git", "rev-parse", "HEAD")
            }.standardOutput.asText.getOrElse("").trim().take(40)}\"",
        )
        // Disabled until this fork has its own signed GitHub release channel.
        // This prevents installing or downloading upstream APKs by mistake.
        buildConfigField("boolean", "SELF_UPDATE_ENABLED", "false")

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        create("release") {
            // Populated from environment or local.properties for local builds
            // CI uses GHA secrets (see docs/ci-cd.md)
            val storeFilePath = providers.environmentVariable("SIGNING_STORE_FILE").orNull
            if (storeFilePath != null) {
                storeFile = file(storeFilePath)
                storePassword = providers.environmentVariable("SIGNING_STORE_PASSWORD").get()
                keyAlias = providers.environmentVariable("SIGNING_KEY_ALIAS").get()
                keyPassword = providers.environmentVariable("SIGNING_KEY_PASSWORD").get()
            }
        }
        create("sharedDebug") {
            // A committed debug keystore so local and CI dev builds share the
            // same signature — clean upgrades between local installs and GitHub
            // dev releases without INSTALL_FAILED_UPDATE_INCOMPATIBLE.
            // Safe to commit: debug credentials are public and this only signs
            // the .debug applicationIdSuffix variant, never releases.
            storeFile = file("debug.keystore")
            storePassword = "android"
            keyAlias = "androiddebugkey"
            keyPassword = "android"
        }
    }

    buildTypes {
        debug {
            isMinifyEnabled = false
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-dev"
            signingConfig = signingConfigs.findByName("sharedDebug")
        }
        release {
            isMinifyEnabled = false
            isShrinkResources = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro",
            )
            signingConfig = signingConfigs.findByName("debug")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlin {
        compilerOptions {
            jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17)
        }
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    lint {
        // Allow incremental localization — new strings may not be translated yet
        disable += "MissingTranslation"
    }

    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }

    testOptions {
        unitTests {
            isIncludeAndroidResources = true
        }
    }
}

@OptIn(com.github.takahirom.roborazzi.ExperimentalRoborazziApi::class)
roborazzi {
    generateComposePreviewRobolectricTests {
        enable = true
        packages = listOf("com.dav3.immichframe")
        includePrivatePreviews = true
        robolectricConfig =
            mapOf(
                "sdk" to "[34]",
                "application" to "android.app.Application::class",
            )
    }
    outputDir.set(file("build/outputs/roborazzi"))
}

dependencies {
    // Kotlin stdlib — force-consistent version to prevent transitive conflicts
    // (Room/KSP pull in old stdlib; AGP 9.1.0 binary-store serialization fails
    // on the constraint graph if versions are inconsistent)
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:${libs.versions.kotlin.get()}"))

    // AndroidX core
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.activity.compose)

    // Compose
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.compose.ui)
    implementation(libs.androidx.compose.ui.graphics)
    implementation(libs.androidx.compose.ui.tooling.preview)
    implementation(libs.androidx.compose.material3)
    implementation(libs.androidx.compose.material.icons.extended)
    debugImplementation(libs.androidx.compose.ui.tooling)

    // Lifecycle
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.lifecycle.viewmodel.compose)

    // Navigation
    implementation(libs.androidx.navigation.compose)

    // Hilt
    implementation(libs.hilt.android)
    ksp(libs.hilt.compiler)
    implementation(libs.androidx.hilt.navigation.compose)
    implementation(libs.androidx.hilt.work)
    ksp(libs.androidx.hilt.compiler)

    // Retrofit + OkHttp
    implementation(libs.retrofit)
    implementation(libs.okhttp)
    implementation(libs.okhttp.logging)

    // Kotlinx Serialization
    implementation(libs.kotlinx.serialization.json)
    implementation(libs.retrofit.kotlinx.serialization)

    // Coil
    implementation(libs.coil.compose)
    implementation(libs.coil.network.okhttp)

    // DataStore
    implementation(libs.androidx.datastore.preferences)

    // Encrypted preferences
    implementation(libs.androidx.security.crypto)

    // Media3 (ExoPlayer) for video playback
    implementation(libs.androidx.media3.exoplayer)
    implementation(libs.androidx.media3.ui)
    implementation(libs.androidx.palette)

    // WorkManager for background sync
    implementation(libs.androidx.work.runtime.ktx)

    // Room for local media database
    implementation(libs.androidx.room.runtime)
    implementation(libs.androidx.room.ktx)
    ksp(libs.androidx.room.compiler)

    // Biometric authentication (fingerprint / face / device credential)
    implementation(libs.androidx.biometric)

    // Custom Tabs (OAuth browser flow during setup)
    implementation(libs.androidx.browser)

    // Screenshot testing (JVM — renders Compose Previews without an emulator)
    testImplementation(libs.junit)
    testImplementation(libs.okhttp.mockwebserver)
    testImplementation(libs.androidx.room.testing)
    testImplementation(libs.robolectric)
    testImplementation(libs.roborazzi)
    testImplementation(libs.roborazzi.compose)
    testImplementation(libs.roborazzi.junit.rule)
    testImplementation(libs.composable.preview.scanner)
    testImplementation("io.github.takahirom.roborazzi:roborazzi-compose-preview-scanner-support:${libs.versions.roborazzi.get()}")
    // Compose tooling + preview support needed in test for @Preview rendering
    testImplementation(libs.androidx.compose.ui.tooling.preview)
    testImplementation("androidx.compose.material3:material3")
    testImplementation("androidx.compose.ui:ui-test-junit4")
    testImplementation("androidx.compose.ui:ui-test-manifest")
}
